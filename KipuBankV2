// SPDX-License-Identifier: MIT
pragma solidity ^0.8.26;

/**
 * @title KipuBank
 * @author Neddy Etman Choque Flores
 * @notice Contrato de bóveda personal que permite a los usuarios depositar y retirar ETH
 * @dev Implementa el patrón checks-effects-interactions y utiliza errores personalizados
 */
contract KipuBank {
    
    /*//////////////////////////////////////////////////////////////
                            VARAIBLES DE ESTADO
    //////////////////////////////////////////////////////////////*/
    
    /// @notice Límite máximo de depósitos que puede contener el banco
    /// @dev Establecido en el constructor, no puede ser modificado
    uint256 public immutable bankCap;
    
    /// @notice Límite máximo de retiro permitido por transacción
    /// @dev Esta variable es inmutable y se establece en el constructor
    uint256 public immutable withdrawalLimit;
    
    /// @notice Balance total almacenado en el contrato
    uint256 public totalDeposits;
    
    /// @notice Contador total de depósitos realizados en el contrato
    uint256 public depositCount;
    
    /// @notice Contador total de retiros realizados en el contrato
    uint256 public withdrawalCount;
    
    /// @notice Mapeo que registra el balance de cada usuario
    /// @dev address => cantidad depositada
    mapping(address => uint256) private userVaults;
    
    /*//////////////////////////////////////////////////////////////
                                EVENTOS
    //////////////////////////////////////////////////////////////*/
    
    /// @notice Emitido cuando un usuario realiza un depósito exitoso
    /// @param user Dirección del usuario que depositó
    /// @param amount Cantidad de ETH depositada
    /// @param newBalance Nuevo balance del usuario después del depósito
    event DepositMade(address indexed user, uint256 amount, uint256 newBalance);
    
    /// @notice Emitido cuando un usuario realiza un retiro exitoso
    /// @param user Dirección del usuario que retiró
    /// @param amount Cantidad de ETH retirada
    /// @param remainingBalance Balance restante del usuario después del retiro
    event WithdrawalMade(address indexed user, uint256 amount, uint256 remainingBalance);
    
    /*//////////////////////////////////////////////////////////////
                            ERRORES PERSONALIZADOS
    //////////////////////////////////////////////////////////////*/
    
    /// @notice Error cuando el monto del depósito es cero
    error KipuBank__DepositAmountMustBeGreaterThanZero();
    
    /// @notice Error cuando el depósito excede el límite del banco
    /// @param attempted Cantidad que se intentó depositar
    /// @param available Espacio disponible en el banco
    error KipuBank__DepositExceedsBankCap(uint256 attempted, uint256 available);
    
    /// @notice Error cuando el usuario intenta retirar más de lo que tiene
    /// @param requested Cantidad solicitada para retirar
    /// @param available Balance disponible del usuario
    error KipuBank__InsufficientBalance(uint256 requested, uint256 available);
    
    /// @notice Error cuando el monto de retiro excede el límite por transacción
    /// @param requested Cantidad solicitada
    /// @param limit Límite máximo permitido
    error KipuBank__WithdrawalExceedsLimit(uint256 requested, uint256 limit);
    
    /// @notice Error cuando la transferencia de ETH falla
    error KipuBank__TransferFailed();
    
    /// @notice Error cuando el monto de retiro es cero
    error KipuBank__WithdrawalAmountMustBeGreaterThanZero();
    
    /*//////////////////////////////////////////////////////////////
                            CONSTRUCTOR
    //////////////////////////////////////////////////////////////*/
    
    /**
     * @notice Inicializa el contrato KipuBank con los límites establecidos
     * @param _bankCap Límite máximo total de depósitos del banco
     * @param _withdrawalLimit Límite máximo de retiro por transacción
     */
    constructor(uint256 _bankCap, uint256 _withdrawalLimit) {
        bankCap = _bankCap;
        withdrawalLimit = _withdrawalLimit;
    }
    
    /*//////////////////////////////////////////////////////////////
                            MODIFIECADORES
    //////////////////////////////////////////////////////////////*/
    
    /**
     * @notice Modificador que valida si un monto es mayor que cero
     * @param amount Monto a validar
     */
    modifier greaterThanZero(uint256 amount) {
        if (amount == 0) {
            revert KipuBank__DepositAmountMustBeGreaterThanZero();
        }
        _;
    }
    
    /*//////////////////////////////////////////////////////////////
                        FUNCIONES EXTERNAS
    //////////////////////////////////////////////////////////////*/
    
    /**
     * @notice Permite a los usuarios depositar ETH en su bóveda personal
     * @dev Implementa el patrón checks-effects-interactions
     * @dev Debe enviarse ETH junto con la transacción (payable)
     */
    function deposit() external payable greaterThanZero(msg.value) {
        // CHECKS: Validar que no exceda el límite del banco
        uint256 availableSpace = bankCap - totalDeposits;
        if (msg.value > availableSpace) {
            revert KipuBank__DepositExceedsBankCap(msg.value, availableSpace);
        }
        
        // EFFECTS: Actualizar el estado antes de interacciones externas
        userVaults[msg.sender] += msg.value;
        totalDeposits += msg.value;
        depositCount++;
        
        uint256 newBalance = userVaults[msg.sender];
        
        // INTERACTIONS: Emitir evento (no hay transferencias aquí)
        emit DepositMade(msg.sender, msg.value, newBalance);
    }
    
    /**
     * @notice Permite a los usuarios retirar ETH de su bóveda personal
     * @param amount Cantidad de ETH a retirar
     * @dev Respeta el límite de retiro por transacción establecido en withdrawalLimit
     */
    function withdraw(uint256 amount) external greaterThanZero(amount) {
        // CHECKS: Validaciones de entrada
        if (amount > withdrawalLimit) {
            revert KipuBank__WithdrawalExceedsLimit(amount, withdrawalLimit);
        }
        
        uint256 userBalance = userVaults[msg.sender];
        if (amount > userBalance) {
            revert KipuBank__InsufficientBalance(amount, userBalance);
        }
        
        // EFFECTS: Actualizar el estado antes de transferir
        userVaults[msg.sender] -= amount;
        totalDeposits -= amount;
        withdrawalCount++;
        
        uint256 remainingBalance = userVaults[msg.sender];
        
        // INTERACTIONS: Transferencia segura al final
        _safeTransferETH(msg.sender, amount);
        
        emit WithdrawalMade(msg.sender, amount, remainingBalance);
    }
    
    /**
     * @notice Consulta el balance de la bóveda de un usuario específico
     * @param user Dirección del usuario a consultar
     * @return Balance actual del usuario en el contrato
     */
    function getVaultBalance(address user) external view returns (uint256) {
        return userVaults[user];
    }
    
    /**
     * @notice Consulta el balance de la bóveda del usuario que llama la función
     * @return Balance actual del msg.sender en el contrato
     */
    function getMyVaultBalance() external view returns (uint256) {
        return userVaults[msg.sender];
    }
    
    /**
     * @notice Retorna el espacio disponible restante en el banco
     * @return Cantidad de ETH que aún puede ser depositada
     */
    function getAvailableSpace() external view returns (uint256) {
        return bankCap - totalDeposits;
    }
    
    /*//////////////////////////////////////////////////////////////
                        FUNCIONES PRIVADAS
    //////////////////////////////////////////////////////////////*/
    
    /**
     * @notice Transfiere ETH de forma segura a una dirección
     * @param to Dirección de destino
     * @param amount Cantidad de ETH a transferir
     * @dev Utiliza call en lugar de transfer para evitar problemas con límite de gas
     * @dev Revierte si la transferencia falla
     */
    function _safeTransferETH(address to, uint256 amount) private {
        (bool success, ) = payable(to).call{value: amount}("");
        if (!success) {
            revert KipuBank__TransferFailed();
        }
    }
}
